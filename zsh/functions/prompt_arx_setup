prompt_arx_help() {
  cat <<\eof
justdoit(tm)
eof
}

prompt_arx_setup() {
  local localhost vcs_info
  zstyle -a ':prompt:arx:' hosts localhost
  zstyle -a ':prompt:arx:' users name

  if  (( localhost[(I)$HOST] )) &&
      ! (( $#parameters[(I)SSH_(CLIENT|TTY|CONNECTION)] )) &&
      ! (( UID == 0 )); then
    PROMPT="%F{6}%B::%b%f "
  else
    PROMPT='%F{6}%m%f %F{6}%B%(!.%#.::)%b%f '
  fi

  if ! (( name[(I)${(%):-%n}] )); then
    PROMPT="%F{6}%n%B%F{6}|%b$PROMPT"
  fi

  zstyle -a ':prompt:arx:vcs_info:' hosts vcs_info
  if (( vcs_info[(I)$HOST] )); then
    zstyle ':vcs_info:*'     enable git
    zstyle ':vcs_info:git:*' formats '%B%F{5}::%%b %F{6}%b%F{4}%f'
    zstyle ':vcs_info:git:*' actionformats '%B%F{5}::%%b %F{6}%b %B%F{4}::%%b %F{6}%a%f'
    autoload vcs_info; $_
    RPROMPT='%B%F{6}%~%f%b ${vcs_info_msg_0_}'
    precmd() vcs_info
  else
    RPROMPT='%B%F{6}%~%f%b'
  fi
}

prompt_arx_setup "$@"

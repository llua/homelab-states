prompt_arx_help() {
  cat <<\eof
justdoit(tm)
eof
}

prompt_arx_setup() {
  local localhost vcs_info c=24 p=45 vp=97
  zstyle -a ':prompt:arx:' hosts localhost
  zstyle -a ':prompt:arx:' users name

  (( $(tput colors) == 8 )) && (( c = p = 6, vp = 5 ))

  if  (( localhost[(I)$HOST] )) &&
      ! (( $#parameters[(I)SSH_(CLIENT|TTY|CONNECTION)] )) &&
      ! (( UID == 0 )); then
    PROMPT="%F{$p}%B::%b%f "
  else
    PROMPT="%F{$c}%m%f %F{$p}%B%(!.%#.::)%b%f "
  fi

  if ! (( name[(I)${(%):-%n}] )); then
    PROMPT="%F{$c}%n%B%F{$p}|%b$PROMPT"
  fi

  zstyle -a ':prompt:arx:vcs_info:' hosts vcs_info
  if (( vcs_info[(I)$HOST] )); then
    zstyle ':vcs_info:*'     enable git
    zstyle ':vcs_info:git:*' formats "%B%F{$vp}::%%b %F{$c}%b%f"
    zstyle ':vcs_info:git:*' actionformats "%B%F{$vp}::%%b %F{$c}%b %B%F{$vp}::%%b %F{$c}%a%f"
    autoload vcs_info; $_
    RPROMPT="%B%F{$p}%~%f%b \${vcs_info_msg_0_}"
    if (( $+functions[precmd] == 0 )) || (( $+parameters[precmd_functions] == 0 )); then
      precmd() vcs_info
    fi
  else
    RPROMPT="%B%F{$p}%~%f%b"
  fi
}

prompt_arx_setup "$@"
